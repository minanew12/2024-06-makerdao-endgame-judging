Refined Scarlet Seagull

Medium

# `VoteDelegate`'s `reserveHatch` allows multi-block MEV to grief LSE users during time-sensitive voting periods

### Summary

LockstakeEngine (LSE) and VoteDelegate (VD) interaction allows malicious actors to exploit multi-block MEV (MMEV) which is possible for roughly 30% of blocks. This allows griefing users of significant funds, and disrupting time-sensitive governance functions. By manipulating VD's `reserveHatch()`(RH) in the first block of MMEV, attackers can cause users' LSE transactions (`selectVoteDelegate`, `lock`, `lockNgt`) to fail during crucial voting periods, potentially leading to losses exceeding 0.5%, or even 5% of user value in gas fees. This exploit not only results in financial losses but also prevents time-sensitive voting delegations, manipulating governance outcomes, especially in emergency situations. The non-upgradeable nature of the system amplifies the long-term impact given MMEV.

The cost to attacker is minimal: 28K gas, while the cost to user is much larger, e.g., 900K gas for a large multicall batch. At 200 gwei and 3500 USD per ETH, this translates to attacker cost (`3500×28000×200÷10^9`): 19 USD, and user cost of 630 USD (more than 5% for 10K user value) in the case of a large multicall batch. 

### Root Cause

The VoteDelegate contract includes a [`reserveHatch` (RH) mechanism that, when called, prevents locking (entering) for a set number of blocks (5)](https://github.com/sherlock-audit/2024-06-makerdao-endgame/blob/main/vote-delegate/src/VoteDelegate.sol#L86-L87), starting from the next block. This prevents single block MEV griefing. 

```solidity
function lock(uint256 wad) external {
    require(block.number == hatchTrigger || block.number > hatchTrigger + HATCH_SIZE,
            "VoteDelegate/no-lock-during-hatch");
    ...
}
```


However, this does not prevent MMEV: MEV over consecutive blocks controlled by a single builder or proposer. This is exacerbated by the [use of Multicall](https://github.com/sherlock-audit/2024-06-makerdao-endgame/blob/main/lockstake/src/LockstakeEngine.sol#L66) which will cause multiple batched user actions to all fail, wasting a very high amount of gas fees.

The `LockstakeEngine` uses this lock function in its[ `_selectVoteDelegate` internal method](https://github.com/sherlock-audit/2024-06-makerdao-endgame/blob/main/lockstake/src/LockstakeEngine.sol#L281), that's used throughout  `selectVoteDelegate`, `lock`, and `lockNgt` user facing functions:

```solidity
function _selectVoteDelegate(address urn, uint256 wad, address prevVoteDelegate, address voteDelegate) internal {
    if (wad > 0) {
	    // ...
        if (voteDelegate != address(0)) {
            // ...
            VoteDelegateLike(voteDelegate).lock(wad);
        }
    }
    // ...
}
```

This design allows an attacker to exploit the `reserveHatch` mechanism to prevent users from executing these functions, particularly during critical voting periods. 

This griefing vector is exacerbated by the fact that a Multicall batch, carrying potentially several gas expensive user actions, will fail when one of the vulnerable actions is blocked. This is because calling RH can be as cheap as 28K gas, but the loss for the user can be much higher, potentially Millions in gas units (depending on the batched operations). This creates a very high griefing factor (cost to damage ratio) of e.g., 3200% griefing ratio (damage / cost) for a 911K batch (from the PoC).

The vulnerability is enabled by the current state of Ethereum's block production, where [a small number of builders](https://www.relayscan.io/) control a large portion of block production (**54% for beaverbuild alone**), enabling multi-block MEV attacks via builders (controlling many consecutive blocks) as well as individual proposers (controlling significant stake and occasional consecutive blocks). For example coinbase controlled proposers control 15% of the stake right now. 

If a builder controls 54% of the blocks, the chances of the next block being controlled by them is 54%, which translates to **29% for any given block**. Thus, if supported by beaverbuild, MMEV would enable this vector for **29% of all blocks**. If MMEV becomes widely adopted by MEV infra software, this could rise to as much 60%, since MEV infra is used by around 80% of stake.

While this finding focuses on `selectVoteDelegate`, `lock`, and `lockNgt`, it's worth noting that `free`, `freeNGT` can also be blocked by frontrunning with a deposit into the `VoteDelegate` due `cheif`'s flashloan protection. This opens even more MMEV options, due to the ability to block multicalls selectively, by either targeting `selectVD / lock` actions, or targetting the `free` actions for different batches. Both attack paths are linked via the tradeoff imposed by the `reserveHatch` and `cheif` functionality between only being able to either free or lock during a single block.

### Internal pre-conditions

- Target VD's RH was not called prior to this block. This is highly likely, since calling RH must be planned, and requires the user to then wait for 6 blocks (more than a minute) before sending their intended transactions (due to the block). 

### External pre-conditions

- A builder supporting MMEV, OR a single proposer controls two consecutive blocks.
- Gas prices are high.

### Attack Path

Scenario 1: Builder-based MMEV Attack

1. An attacker observes queued mempool multicall transactions involving delegation updates in LSE during a time-sensitive voting period.
2. The attacker users a builder supporting MMEV to execute the following:
    - Block N: Include the attacker's `reserveHatch()` call to the target VoteDelegate.
    - Block N+1: Include the user's multicall transaction, which will fail when it tries to execute `lock()` in the VoteDelegate.

Scenario 2: Proposer-based MMEV Attack

1. A malicious proposer is scheduled to propose two consecutive blocks during a time-sensitive voting period and observes vulnerable transactions in the mempool.
2. The proposer executes:
    - Block N: Include the attacker's `reserveHatch()` call.
    - Block N+1: Include the user's transaction, which will fail.

Scenario 3: Selective Governance Attack

1. A controversial or emergency governance proposal is submitted.
2. The attacker repeatedly executes the `reserveHatch` attack during the voting period when possible (outside of cooldown periods) against users trying to vote against the attacker's proposal.
3. Many users will have their transactions fail if not submitted during RH cooldown periods.
4. This griefs a large amount of user, increases the user costs for governance participation for one outcome over the over, increasing the chance of the attacker influencing the vote.
5. This delays users' time-sensitive vote delegations in times of emergency.

### Impact

1. Loss of funds: Users lose significant gas fees when their time-sensitive transactions fail. Depending on gas prices and complexity of multicall transactions, losses could amount to hundreds of dollars per failed transaction. For example, at 3500 USD per ETH, and 200 gwei gas price:
	- Attack cost: 28K gas, 19 USD.
	- Large multicall revert cost (PoC - 8 ops): 911K gas, 637 USD
	- Smaller multicall revert cost (PoC - 5 ops): 709K gas, 496 USD
	- No multicall, islocated `selectVoteDelegate` revert: 101K gas, 70 USD
		   
2. Governance Interference: The attack can prevent users from updating their vote delegations during critical periods, potentially swaying governance decisions. This is particularly impactful for emergency proposals that require quick action.
3. Long-term Participation Decline: Consistent attacks could discourage users from participating in governance, leading to more vulnerable governance due to lower honest user participation.
4. Systemic Risk: Given that the LockstakeEngine is designed to be non-upgradeable and used for multiple years, this vulnerability poses a long-term risk to the system's integrity and decentralization due to the likelihood of MMEV prevalence.

### PoC

The PoC measures the gas costs for: 
1. Calling `reserveHatch` (attacker): 28K gas
2. Gas costs for the griefed user for isolated `selectVoteDelegate` revert: 101K gas
3. Gas costs for a 5 ops multicall batch revert: 709K gas
4. Gas costs for a 8 ops multicall batch revert: 911K gas

The PoC adds tests for `LockstakeEngineBenchmarks` class in `Benchmarks.t.sol` file in this measurement PR https://github.com/makerdao/lockstake/pull/38/files of branch https://github.com/makerdao/lockstake/tree/benchmarks. I've also merged latest `dev` into it to ensure it's using latest contracts. 

The output of the PoC:
```bash
>>> forge test --mc LockstakeEngineBenchmarks --mt testGas -vvv

Logs:
    reserveHatch cost: 28248
    multicall (5 ops) revert cost: 709731
    multicall (8 ops) revert cost: 911424

Logs:
    reserveHatch cost: 23594
    selectVoteDelegate revert cost: 101133

```

The added tests:
```solidity
function testGasMulticallRevert() public {  
    uint startGas = gasleft();  
    VoteDelegate(voteDelegate).reserveHatch();  
    uint gasUsed = startGas - gasleft();  
    console2.log("  reserveHatch cost:", gasUsed);  
  
    vm.roll(block.number + 1);  
  
    mkr.approve(address(engine), 1_000_000 * 10**18);  
  
    address urn = engine.getUrn(address(this), 0);  
  
    bytes[] memory data = new bytes[](5);  
    data[0] = abi.encodeCall(engine.open, (0));  
    data[1] = abi.encodeCall(engine.lock, (urn, 100_000 * 10**18, 5));  
    data[2] = abi.encodeCall(engine.draw, (urn, address(this), 2_000 * 10**18));  
    data[3] = abi.encodeCall(engine.selectFarm, (urn, address(farm), 0));  
    data[4] = abi.encodeCall(engine.selectVoteDelegate, (urn, voteDelegate));  
  
    vm.expectRevert();  
    startGas = gasleft();  
    engine.multicall(data);  
    gasUsed = startGas - gasleft();  
    console2.log("  multicall (5 ops) revert cost:", gasUsed);  
  
    data = new bytes[](8);  
    data[0] = abi.encodeCall(engine.open, (0));  
    data[1] = abi.encodeCall(engine.selectFarm, (urn, address(farm), 0));  
    data[2] = abi.encodeCall(engine.lock, (urn, 100_000 * 10**18, 5));  
    data[3] = abi.encodeCall(engine.draw, (urn, address(this), 1_000 * 10**18));  
    data[4] = abi.encodeCall(engine.lock, (urn, 50_000 * 10**18, 5)); // simulates lockNgt  
    data[5] = abi.encodeCall(engine.draw, (urn, address(this), 1_000 * 10**18));  
    data[6] = abi.encodeCall(engine.getReward, (urn, address(farm), address(this)));  
    data[7] = abi.encodeCall(engine.selectVoteDelegate, (urn, voteDelegate));  
  
    vm.expectRevert();  
    startGas = gasleft();  
    engine.multicall(data);  
    gasUsed = startGas - gasleft();  
    console2.log("  multicall (8 ops) revert cost:", gasUsed);  
}  
  
function testGasSelectVDRevert() public {  
    address voter2 = address(1234);  
    vm.prank(voter2);  
    address voteDelegate2 = delFactory.create();  
  
    address[] memory yays = new address[](5);  
    for (uint256 i; i < 5; i++) yays[i] = address(uint160(i + 1));  
    vm.prank(voter); VoteDelegate(voteDelegate).vote(yays);  
    vm.prank(voter2); VoteDelegate(voteDelegate2).vote(yays);  
    address urn = _urnSetUp(true, true);  
  
    uint startGas = gasleft();  
    VoteDelegate(voteDelegate2).reserveHatch();  
    uint gasUsed = startGas - gasleft();  
    console2.log("  reserveHatch cost:", gasUsed);  
    vm.roll(block.number + 1);  
  
    vm.expectRevert();  
    startGas = gasleft();  
    engine.selectVoteDelegate(urn, voteDelegate2);  
    gasUsed = startGas - gasleft();  
    console2.log("  selectVoteDelegate revert cost:", gasUsed);  
}
```


### Mitigation

To mitigate this issue, the VoteDelegate contract's `reserveHatch` mechanism should be modified to introduce a longer delay before the hatch takes effect. Instead of starting from the next block, it should start after M blocks, where M is a governance-controlled parameter. The parameters can be stored on the VD factory, and the VDs can query it there. This makes the attack more expensive and less likely to succeed, as controlling M consecutive blocks is more expensive and less likely.

The total time the hatch is open and the duration of the cooldown cycle is not changed. The only modification is the timing of the hatch in the cycle. Instead of the cycle being 25 blocks starting with the 5 hatch blocks, the 5 hatch blocks are moved later in the cycle.

Proposed change:

```diff
function lock(uint256 wad) external {
-        require(block.number == hatchTrigger || block.number > hatchTrigger + HATCH_SIZE, "VoteDelegate/no-lock-during-hatch");
+        uint256 hatchDelay = factory.getHatchDelay(); // set by gov on the factory
+        bool beforeHatch = block.number < hatchTrigger + hatchDelay;
+        bool afterHatch = block.number > hatchTrigger + hatchDelay + HATCH_SIZE;
+        require( beforeHatch || afterHatch, "VoteDelegate/no-lock-during-hatch");
        ...
    }
```

Governance can adjust `hatchDelay` based on the likelihood of MMEV issues.